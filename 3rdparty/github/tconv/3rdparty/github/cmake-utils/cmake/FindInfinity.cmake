MACRO (FINDINFINITY)
  GET_PROPERTY(source_dir_set GLOBAL PROPERTY MYPACKAGE_SOURCE_DIR SET)
  IF (NOT ${source_dir_set})
    MESSAGE (WARNING "Cannot check infinity, property MYPACKAGE_SOURCE_DIR is not set")
  ELSE ()
    IF (NOT C_INFINITY_SINGLETON)
      GET_PROPERTY(source_dir GLOBAL PROPERTY MYPACKAGE_SOURCE_DIR)
      SET (_C_INFINITY_FOUND FALSE)
      #
      # We depend on math.h
      #
      INCLUDE (CheckIncludeFile)
      CHECK_INCLUDE_FILE ("math.h" HAVE_MATH_H)
      IF (HAVE_MATH_H)
        SET (_HAVE_MATH_H 1)
      ELSE ()
        SET (_HAVE_MATH_H 0)
      ENDIF ()
      #
      # Test
      #
      MESSAGE(STATUS "Looking for working INFINITY")
      TRY_COMPILE (C_HAS_INFINITY ${CMAKE_CURRENT_BINARY_DIR}
        ${source_dir}/INFINITY.c
        COMPILE_DEFINITIONS -DC_INFINITY=INFINITY -DHAVE_MATH_H=${_HAVE_MATH_H})
      IF (C_HAS_INFINITY)
        MESSAGE(STATUS "Looking for working INFINITY - found")
        SET (_C_INFINITY INFINITY)
        SET (_C_INFINITY_FOUND TRUE)
      ELSE ()
        MESSAGE(STATUS "Looking for INFINITY replacement")
        TRY_COMPILE (C_HAS_INFINITY_REPLACEMENT ${CMAKE_CURRENT_BINARY_DIR}
          ${source_dir}/INFINITY.c
          COMPILE_DEFINITIONS -DHAVE_INFINITY_REPLACEMENT=1 -DHAVE_MATH_H=${_HAVE_MATH_H})
        IF (C_HAS_INFINITY_REPLACEMENT)
          MESSAGE(STATUS "Looking for INFINITY replacement - found")
          SET (_C_INFINITY_REPLACEMENT TRUE)
        ELSE ()
          MESSAGE(STATUS "Looking for INFINITY replacement using division")
          TRY_RUN(C_INFINITY_RUN_RESULT
            C_INFINITY_COMPILE_RESULT
            ${CMAKE_CURRENT_BINARY_DIR}
            ${source_dir}/INFINITY.c
            COMPILE_DEFINITIONS -DHAVE_INFINITY_REPLACEMENT_USING_DIVISION=1 -DHAVE_MATH_H=${_HAVE_MATH_H})
          IF (C_INFINITY_COMPILE_RESULT AND (C_INFINITY_RUN_RESULT EQUAL 0))
            MESSAGE(STATUS "Looking for INFINITY replacement using division - found")
            SET (_C_INFINITY_REPLACEMENT_USING_DIVISION TRUE)
          ENDIF ()
        ENDIF ()
      ENDIF ()
    ENDIF ()
    IF (_C_INFINITY_FOUND)
      SET (C_INFINITY "${_C_INFINITY}" CACHE STRING "C INFINITY implementation")
      MARK_AS_ADVANCED (C_INFINITY)
    ENDIF ()
    IF (_C_INFINITY_REPLACEMENT)
      SET (C_INFINITY_REPLACEMENT "(__builtin_inff())" CACHE STRING "C INFINITY_REPLACEMENT implementation")
      MARK_AS_ADVANCED (C_INFINITY_REPLACEMENT)
    ENDIF ()
    IF (_C_INFINITY_REPLACEMENT_USING_DIVISION)
      SET (C_INFINITY_REPLACEMENT_USING_DIVISION "(1.0/0.0)" CACHE STRING "C INFINITY_REPLACEMENT_USING_DIVISION implementation")
      MARK_AS_ADVANCED (C_INFINITY_REPLACEMENT_USING_DIVISION)
    ENDIF ()
    SET (C_INFINITY_SINGLETON TRUE CACHE BOOL "C INFINITY check singleton")
    MARK_AS_ADVANCED (C_INFINITY_SINGLETON)
  ENDIF ()
ENDMACRO()
